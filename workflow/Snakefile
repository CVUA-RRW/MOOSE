import os
import shutil
from snakemake.utils import min_version
from snakemake.utils import validate


# Settings --------------------------------------------------------------------

min_version("6.3.0")

shell.executable("bash")


configfile: os.path.join(workflow.basedir, "..", ".tests", "config", "config.yaml")


# Validating samples in rules/common


workdir: config["workdir"]


# report: "report/workflow.rst"

# Input rules ------------------------------------------------------------------


include: "rules/common.smk"


rule all:
    input:
        # trimming
        "reports/fastp_stats.tsv",
        # host mapping
        expand("{sample}/host_mapping/{sample}_host_aln.bam", sample=samples.index),
        expand("{sample}/host_mapping/{sample}_host_coverage.tsv", sample=samples.index),
        expand("{sample}/host_mapping/{sample}_host_mapstats.tsv", sample=samples.index),
        expand("{sample}/host_mapping/{sample}_host_flagstats.tsv", sample=samples.index),
        expand("{sample}/host_mapping/{sample}_host_rpkm.tsv", sample=samples.index),
        # assembly
        # expand("{sample}/assembly/quast/report.tsv", sample=samples.index),
        expand("{sample}/assembly/{sample}_contigs.fasta", sample=samples.index),
        # expand("{sample}/assembly/assembly_graph_connected.gfa", sample=samples.index),
        # blast
        expand("{sample}/blast/{sample}_blast.tsv" , sample=samples.index),
        expand("{sample}/blast/{sample}_events.tsv" , sample=samples.index),
        # coverage
        expand("{sample}/assembly/{sample}_aln.bam", sample=samples.index),
        expand("{sample}/assembly/{sample}_coverage.tsv", sample=samples.index),
        expand("{sample}/assembly/{sample}_coverage_depth.tsv", sample=samples.index),
        # expand("{sample}/assembly/{sample}_rpkm.tsv", sample=samples.index),
        # reports
        expand("{sample}/reports/{sample}_hosts_detection.png", sample=samples.index),
        expand("{sample}/reports/blast_graph_artifact", sample=samples.index),
        # expand( , sample=samples.index),


# Includes --------------------------------------------------------------------


include: "rules/trimming.smk"
include: "rules/host_mapping.smk"
include: "rules/contigs.smk"
include: "rules/blast.smk"
# include: "rules/coverage.smk"
include: "rules/reports.smk"


# Workflow --------------------------------------------------------------------


onstart:
    print(f"\nYou are using MOOSE version: {version}")
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline started\n")


onsuccess:
    try:
        for logfile in os.listdir(".snakemake/log/"):
            shutil.move(os.path.join(".snakemake/log", logfile), "logs")
        shutil.rmtree(".snakemake", ignore_errors=True)
    except:
        # if not executing .snakemake from workdir,
        # the log file will be in execution directory
        # as far as I know, there is now way to access this form here
        pass
    print("\nWorkflow finished, no error")
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline succesfully finished\n")


onerror:
    print(
        f"\nAn error occured, please consider filing an bug report."
        f"\n"
        f"\nIssue tracker: https://github.com/CVUA-RRW/MOOSE/issues"
    )
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline stopped on error\n")